# Maintainer: Alex Zaslavsky <alex@wall-dev.org>

pkgname=gebaar-libinput-fork-git
pkgver=r47.07b0ca8
pkgrel=1
pkgdesc='A Super Simple WM Independent Touchpad Gesture Daemon for libinput. Forked version with new features'
arch=('x86_64')
url="https://github.com/Osleg/gebaar-libinput-fork"
license=('GPL3')
depends=('libinput')
makedepends=('cmake' 'git')
conflicts=('gebaar')
provides=('gebaar')
source=('git+https://github.com/Osleg/gebaar-libinput-fork')
md5sums=('SKIP')


pkgver() {
  cd "$srcdir/gebaar-libinput-fork"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir/gebaar-libinput-fork
  git submodule init
  git submodule update
  mkdir -p build && cd build
  cmake -DCMAKE_INSTALL_PREFIX="/usr" ..
}

build() {
  cd $srcdir/gebaar-libinput-fork/build
  make -j$(nproc)
}

package() {
  cd $srcdir/gebaar-libinput-fork/build
  make DESTDIR="$pkgdir" install/strip
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
